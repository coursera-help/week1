CREATE EXTERNAL TABLE staged_log
(level STRING,
 datetime STRING,
 source STRING,
 event_id INT,
 category STRING,
 details STRING)
ROW FORMAT DELIMITED FIELDS TERMINATED BY '\t'
STORED AS TEXTFILE LOCATION '/data/staged_log';

LOAD DATA INPATH '/data/system_log.txt' INTO TABLE staged_log;

SELECT * FROM staged_log;

CREATE TABLE system_log
(level STRING,
 datetime STRING,
 source STRING,
 event_id INT,
 category STRING,
 details STRING);

INSERT INTO TABLE system_log
SELECT * FROM staged_log
WHERE event_id IS NOT NULL;

SELECT * FROM system_log;

CREATE TABLE error_log
ROW FORMAT DELIMITED FIELDS TERMINATED BY '\t'
STORED AS TEXTFILE LOCATION '/data/error_log'
AS
SELECT datetime, source, event_id, details
FROM system_log
WHERE level = 'Error';

SELECT * FROM error_log;




-- Querying

SELECT datetime, level, event_id
FROM system_log
LIMIT 10;

SELECT unix_timestamp(datetime, 'dd/MM/yyyy hh:mm:ss') AS time_stamp, level, event_id
FROM system_log;

SELECT from_unixtime(unix_timestamp(datetime, 'dd/MM/yyyy hh:mm:ss')) AS datetime, level, event_id
FROM system_log;


CREATE VIEW v_system_log
AS
SELECT from_unixtime(unix_timestamp(datetime, 'dd/MM/yyyy hh:mm:ss')) AS datetime, level, event_id, source, details
FROM system_log;

SELECT datetime, level, event_id
FROM v_system_log
LIMIT 10;

SELECT CAST(SUBSTR(datetime, 1, 10) AS DATE) AS event_date, level, event_id
FROM v_system_log;


SELECT CAST(SUBSTR(datetime, 1, 10) AS date) AS event_date, level, event_id
FROM v_system_log
ORDER BY event_date;

SELECT CAST(SUBSTR(datetime, 1, 10) AS date) AS event_date, COUNT(*) AS events
FROM v_system_log
GROUP BY CAST(SUBSTR(datetime, 1, 10) AS date)
ORDER BY event_date;

SELECT CAST(SUBSTR(datetime, 1, 10) AS date) AS event_date, level, COUNT(*) AS events
FROM v_system_log
GROUP BY CAST(SUBSTR(datetime, 1, 10) AS date), level
ORDER BY event_date, level;



-- Partitioned Table

CREATE TABLE part_log
(event_date DATE,
 source STRING,
 event_id INT,
 details STRING)
PARTITIONED BY (level STRING)
ROW FORMAT DELIMITED FIELDS TERMINATED BY '\t'
STORED AS TEXTFILE LOCATION '/data/part_log';

SET hive.exec.dynamic.partition.mode=nonstrict;
SET hive.exec.dynamic.partition = true;
INSERT INTO TABLE part_log PARTITION(level)
SELECT CAST(SUBSTR(datetime, 1, 10) AS DATE), source, event_id, details, level
FROM v_system_log;

SELECT event_date, event_id
FROM part_log
WHERE level = 'Error';



